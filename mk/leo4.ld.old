/* L4Xpresso memory area definitions 
*/

INCLUDE "../mk/leo4_mem.ld"

ENTRY (__l4_start)

/*
 * Memory Relocation 
 * 
 * +-------------------+	0x000000000 
 * +  isr_vectors      + 64k
 * +  text / data      +
 * +--------------------	0x000010000 (+KERNEL_TEXT)
 * |....Userspace......|
 * ~~~~~~~~~~~~~~~~~~~~~
 *
 * +-------------------+	0x100000000 
 * +  stack            + 2k	
 * +-------------------+	0x100000800 (+KERNEL_STACK)
 * +  data / bss       + 10k
 * ~~~~~~~~~~~~~~~~~~~~~	0x100003000 (+KERNEL_DATA)
 * |....Userspace......|
 * ~~~~~~~~~~~~~~~~~~~~~    0x10007c000 (+USER_DATA) 
 * +  KIP              + 1k
 * +-------------------+
 *
 * 
 * +-------------------+	0x2007C000
 * +  AHB mem          +   
 * +-------------------+
 * 
 *
 */

KERNEL_TEXT_SIZE	 = 0x00010000;
KERNEL_STACK_SIZE    = 0x00000800;
KERNEL_DATA_SIZE	 = 0x00002800;

KERNEL_STACK_ADDR = 0x10000000;
KERNEL_DATA_ADDR = 0x100000800;
KERNEL_KIP_ADDR = 0x10007c000;

SECTIONS {
	.text 0x00000000:
	{
		KEEP(*(.isr_vector))
		*(.text*)
		*(.rodata*)
	} > MFlash512
	kernel_text_end = .;
	
	.stack :
	{
		. += KERNEL_STACK_SIZE;
		kernel_stack_addr = .;
	} > RamLoc32
	
	.data :
	{
		kernel_data_start = .;
		/*Only kernel data*/
		*(.data*)
		*(.ktable*)			/*Statically allocated kernel table*/
		kernel_data_end = .;
	} >RamLoc32  AT>MFlash512

	/* zero initialized data */
	.bss :
	{
		kernel_bss_start = .;
		*(.bss*)
		*(COMMON)
		kernel_bss_end = .;
	} > RamLoc32
	
	.kip 0x10007c00:
	{
		kernel_kip_start = .;
		*(*.kip)
		kernel_kip_end = .;
	} >RamLoc32  AT>MFlash512
}